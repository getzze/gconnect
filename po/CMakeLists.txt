#
#
# Translations

# List of languages this project has been translated into.  Each
# language should match a ${LANGUAGE}.po file in this directory.
set(translated_languages)

find_package(Gettext REQUIRED)

# Find sources to use process.
file(GLOB gettext_source_files
    ${CMAKE_SOURCE_DIR}/my-project/*.vala
    ${CMAKE_SOURCE_DIR}/my-project/*.c
    ${CMAKE_SOURCE_DIR}/executables/*.vala
    ${CMAKE_SOURCE_DIR}/executables/*.c
    ${CMAKE_SOURCE_DIR}/gui/*.vala
    ${CMAKE_SOURCE_DIR}/gui/resources/*.ui)

# Create an "update-pot" target.  This will scan the sources and
# update the list of translatable strings.  Note that this will output
# to the source directory, not the build directory.
find_program(XGETTEXT xgettext)
if(NOT "${XGETTEXT}" STREQUAL "XGETTEXT-NOTFOUND")
    add_custom_command(OUTPUT ${CMAKE_CURRENT_SOURCE_DIR}/messages.pot
        COMMAND ${XGETTEXT}
            -o "${CMAKE_CURRENT_SOURCE_DIR}/messages.pot"
            --add-comments
            --package-name "${PROJECT_NAME}"
            --package-version "${MY_PROJECT_VERSION_MAJOR}.${MY_PROJECT_VERSION_MINOR}.${MY_PROJECT_VERSION_REVISION}"
            --msgid-bugs-address "${PROJECT_BUGS}"
            ${gettext_source_files}
        DEPENDS ${gettext_source_files})

    add_custom_target(update-pot DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/messages.pot")
endif()

# Create targets to translate each language's po file into an mo file.
set(mo_files)
foreach(language ${translated_languages})
    list(APPEND mo_files "${CMAKE_CURRENT_BINARY_DIR}/${language}.mo")
    add_custom_command(OUTPUT "${language}.mo"
        COMMAND ${GETTEXT_MSGFMT_EXECUTABLE}
            -o "${CMAKE_CURRENT_BINARY_DIR}/${language}.mo"
            "${language}.po"
        DEPENDS "${language}.po"
        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})
    install(FILES "${CMAKE_CURRENT_BINARY_DIR}/${language}.mo"
        RENAME "${GETTEXT_PACKAGE}.mo"
        DESTINATION "${CMAKE_INSTALL_LOCALEDIR}/${language}/LC_MESSAGES/")
endforeach(language)

# Create an "update-mo" target which depends on all the mo files.
add_custom_target(update-mo ALL DEPENDS ${mo_files})

# Generate a .gitignore
file(WRITE  ".gitignore" "# Automatically generated by CMake, do not modify.\n")
foreach(file
    ".gitignore")
  file(APPEND ".gitignore" "/${file}\n")
endforeach(file)
foreach(language ${translated_languages})
  file(APPEND ".gitignore" "/${language}.mo\n")
endforeach(language)
