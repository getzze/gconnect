#
#
# Main target

# Name of the GUI executable
set(MY_PROJECT_GUI_NAME "${PROJECT_NAME}-gui")

find_package(PkgConfig REQUIRED)

pkg_check_modules(GTK REQUIRED gtk+-3.0)

list(APPEND VALA_COMPILER_FLAGS "--gresources=${CMAKE_CURRENT_SOURCE_DIR}/resources/resources.gresource.xml")

# Compile Vala sources to C
vala_precompile_target(
  "${MY_PROJECT_GUI_NAME}-vala"
  MY_PROJECT_GUI_SOURCES
  ${CMAKE_BINARY_DIR}/my-project/my-project-${MY_PROJECT_VERSION_API}.vapi
  gui.vala
  PACKAGES gtk+-3.0
  DEPENDS "${MY_PROJECT_LIBRARY_NAME}-vala")

# Compile resources to C sources
glib_compile_resources("resources/resources.gresource.xml"
  SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/resources"
  SOURCE "${CMAKE_CURRENT_BINARY_DIR}/gui-resources.c"
  C_NAME "my_project_gui_resources"
  INTERNAL)

# Tell CMake to create an executable named "${MY_PROJECT_GUI_NAME}"
add_executable("${MY_PROJECT_GUI_NAME}"
    ${MY_PROJECT_GUI_SOURCES}
    "${CMAKE_CURRENT_BINARY_DIR}/gui-resources.c")

# Make sure the Vala sources are compiled to C before attempting to
# build the executable.
add_dependencies("${MY_PROJECT_GUI_NAME}" "${MY_PROJECT_GUI_NAME}-vala")

# Link to libraries
target_link_libraries(${MY_PROJECT_GUI_NAME}
    ${MY_PROJECT_LIBRARY_NAME}
    ${GTK_LIBRARIES})

# Add GTK+'s include directory
set_property(
    TARGET ${MY_PROJECT_GUI_NAME}
    APPEND PROPERTY INCLUDE_DIRECTORIES
        ${GTK_INCLUDE_DIRS})

# Install the executable.
install (TARGETS ${MY_PROJECT_GUI_NAME}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR})

# Install GSettings schemas
glib_install_schemas(
    "settings/com.github.felipe-lavratti.vala-unittests-cmake.gschema.xml")

# Generate a .gitignore
file(WRITE  ".gitignore" "# Automatically generated by CMake, do not modify.\n")
foreach(file
    ".gitignore"
    "${MY_PROJECT_GUI_NAME}${CMAKE_EXECUTABLE_SUFFIX}")
  file(APPEND ".gitignore" "/${file}\n")
endforeach(file)
foreach(file ${MY_PROJECT_GUI_SOURCES})
  string(REPLACE "${CMAKE_CURRENT_BINARY_DIR}/" "" file ${file})
  file(APPEND ".gitignore" "/${file}\n")
endforeach(file)
